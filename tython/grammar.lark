start: stmts+

stmts: class_structure
     | interface
     | function
     | var

expr: types_repr
    | function_call
    | interface_shape
    | dotted_access
    | new_class
    | 

var: var_declarations id "=" ret_expr terminator
   | dotted_access "=" ret_expr terminator -> reassign

dotted_access: "this" ("." id)* -> this_dotted_access
             | access -> access
             | access ("." access)+

access: id

var_declarations: "let"
            | "var"
            | "const" // TODO: signify importance?

function: "function" id "(" func_args? ")" func_body

func_args: func_arg ("," func_arg)*

func_arg: implicit_or_typed

implicit_or_typed: id -> any_typed
                 | id ":" types -> explicit_type

func_body: "{" func_stmts+ "}"

func_stmts: stmts
          | "return" ret_expr ";" -> return_expr

ret_expr: expr
        | sum

function_call: id "(" call_args ")"

call_args: call_arg ("," call_arg)*

call_arg: ret_expr

types: "any" -> any
     | "string" -> string
     | "boolean" -> boolean
     | "number" -> number
     | id -> object

?sum: product
    | sum "+" product -> add
    | sum "-" product -> min

?product: atom
        | product "*" atom -> mul
        | product "/" atom -> div

?atom: expr
     | id
     | "-" atom -> negative_types_repr
     | "(" sum ")"

types_repr: str
     // number 
     // boolean

interface: "interface" id interface_body

interface_body: "{" interface_declarations+ "}"

interface_declarations: interface_declaration ";"

interface_declaration: implicit_or_typed

interface_shape: "{" interface_shape_args? "}"

interface_shape_args: interface_shape_arg ("," interface_shape_arg)*

interface_shape_arg: id ":" expr

class_structure: "class" id class_body

class_body: "{" class_declarations+ "}"

class_declarations: implicit_or_typed ";" -> field
                  | constructor_method

constructor_method: "constructor" "(" constructor_args? ")" func_body

constructor_args: constructor_arg ("," constructor_arg)*

constructor_arg: func_arg
               | "public" func_arg -> public_arg

new_class: "new" id "(" call_args ")"

str: /(`.*?`)|(".*?")|(\'.*?\')/

terminator: ";"

id: /[a-zA-Z_][a-zA-Z0-9_]*/ -> identifier

%import common.WS
%ignore WS